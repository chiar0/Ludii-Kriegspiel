(define "NextCanNotMove"
    (not
        (can Move
            (do
                (forEach Piece Next)
                ifAfterwards:(not ("KingInCheck" (next)(mover)))
            )
        )
    )
)

(define "HasNeverMoved"
    (= (state at:(mapEntry #1 (mover))) 1)
)

(define "PieceHasMoved"
    (set State at:#1 0)
)

(define "NoteCapture"
    (if (<= (#2) 2)
        (note (mapEntry "CaptureNotePawn" #1))
        (note (mapEntry "CaptureNotePiece" #1))
    )
)

(define "KingGravitatesToMainDiagonal"
    (or
        (and (< (row of:(where "King" Next)) 4) (< (column of:(where "King" Next)) 4))
        (and (>= (row of:(where "King" Next)) 4) (>= (column of:(where "King" Next)) 4))
    )
)

(define "KingGravitatesToAntiDiagonal"
    (or
        (and (< (row of:(where "King" Next)) 4) (>= (column of:(where "King" Next)) 4))
        (and (>= (row of:(where "King" Next)) 4) (< (column of:(where "King" Next)) 4))
    )
)

(define "PawnDiagonalCheck"
    (= (what at: (ahead (where "King" #1) steps:1 #3)) (mapEntry "Pawn" #2))
)

(define "DiagonalCheck"
    (or
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) #3) index: 0)) (mapEntry "Queen" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) #3) index: 0)) (mapEntry "Bishop" #2))
    )
)

(define "FileCheck"
    (or {
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) N) index: 0)) (mapEntry "Queen" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) S) index: 0)) (mapEntry "Queen" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) N) index: 0)) (mapEntry "Rook" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) S) index: 0)) (mapEntry "Rook" #2))
    })
)

(define "RankCheck"
    (or {
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) E) index: 0)) (mapEntry "Queen" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) W) index: 0)) (mapEntry "Queen" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) E) index: 0)) (mapEntry "Rook" #2))
        (= (what at:(regionSite (sites LineOfSight Piece at:(where "King" #1) W) index: 0)) (mapEntry "Rook" #2))
    })
)

(define "KnightCheck"
    (and {
        (= (mapEntry "Knight" #2) (what at:(ahead (ahead (where "King" #1) steps:2 #3) steps:1 #4)))
        (!= (ahead (where "King" #1) steps:2 #3) (ahead (ahead (where "King" #1) steps:2 #3) steps:1 #4))
        (!= (where "King" #1) (ahead (where "King" #1) steps:2 #3))
    })
)

(define "CheckType"
    (and {
        (remember Value "CheckedKingSite" (where "King" (next)) unique:True)
        (if
            (or
                ("DiagonalCheck" (next) (mover) SE)
                (and
                    (= (next) 2)
                    ("PawnDiagonalCheck" (next) (mover) SE)
                )
            )
            (and {
                (forEach Site
                    (sites Direction
                        from:(where "King" (next))
                        SE
                        included:False
                        distance:7
                    )
                    (remember Value "CheckingPieceSite" (site) unique:True)
                )
                (if "KingGravitatesToMainDiagonal" (note "Short diagonal check"))
                (if "KingGravitatesToAntiDiagonal" (note "Long diagonal check"))
            })
        )
        (if
            (or
                ("DiagonalCheck" (next) (mover) SW)
                (and
                    (= (next) 2)
                    ("PawnDiagonalCheck" (next) (mover) SW)
                )
            )
            (and {
                (forEach Site
                    (sites Direction
                        from:(where "King" (next))
                        SW
                        included:False
                        distance:7
                    )
                    (remember Value "CheckingPieceSite" (site) unique:True)
                )
                (if "KingGravitatesToMainDiagonal" (note "Long diagonal check"))
                (if "KingGravitatesToAntiDiagonal" (note "Short diagonal check"))
            })
        )
        (if
            (or
                ("DiagonalCheck" (next) (mover) NE)
                (and
                    (= (next) 1)
                    ("PawnDiagonalCheck" (next) (mover) NE)
                )
            )
            (and {
                (forEach Site
                    (sites Direction
                        from:(where "King" (next))
                        NE
                        included:False
                        distance:7
                    )
                    (remember Value "CheckingPieceSite" (site) unique:True)
                )
                (if "KingGravitatesToMainDiagonal" (note "Long diagonal check"))
                (if "KingGravitatesToAntiDiagonal" (note "Short diagonal check"))
            })
        )
        (if
            (or
                ("DiagonalCheck" (next) (mover) NW)
                (and
                    (= (next) 1)
                    ("PawnDiagonalCheck" (next) (mover) NW)
                )
            )
            (and {
                (forEach Site
                    (sites Direction
                        from:(where "King" (next))
                        NW
                        included:False
                        distance:7
                    )
                    (remember Value "CheckingPieceSite" (site) unique:True)
                )
                (if "KingGravitatesToMainDiagonal" (note "Short diagonal check"))
                (if "KingGravitatesToAntiDiagonal" (note "Long diagonal check"))
            })
        )
        (if ("FileCheck" (next) (mover)) 
            (and {
                (if (> (count Sites in:(sites LineOfSight Piece at:(where "King" (next)) N)) 0)
                    (forEach Site
                        (sites Direction
                            from:(where "King" (next))
                            N
                            included:False
                            distance:7
                        )
                        (remember Value "CheckingPieceSite" (site) unique:True)
                    )
                )
                (if (> (count Sites in:(sites LineOfSight Piece at:(where "King" (next)) S)) 0)
                    (forEach Site
                        (sites Direction
                            from:(where "King" (next))
                            S
                            included:False
                            distance:7
                        )
                        (remember Value "CheckingPieceSite" (site) unique:True)
                    )
                )
                (note "File check")
            })
        )
        (if ("RankCheck" (next) (mover))
            (and {
                (if (> (count Sites in:(sites LineOfSight Piece at:(where "King" (next)) E)) 0)
                    (forEach Site
                        (sites Direction
                            from:(where "King" (next))
                            E
                            included:False
                            distance:7
                        )
                        (remember Value "CheckingPieceSite" (site) unique:True)
                    )
                )
                (if (> (count Sites in:(sites LineOfSight Piece at:(where "King" (next)) W)) 0)
                    (forEach Site
                        (sites Direction
                            from:(where "King" (next))
                            W
                            included:False
                            distance:7
                        )
                        (remember Value "CheckingPieceSite" (site) unique:True)
                    )
                )
                (note "Rank check")
            })
        )
        (if
            (or {
                ("KnightCheck" (next) (mover) N E)
                ("KnightCheck" (next) (mover) N W)
                ("KnightCheck" (next) (mover) W N)
                ("KnightCheck" (next) (mover) W S)
                ("KnightCheck" (next) (mover) S W)
                ("KnightCheck" (next) (mover) S E)
                ("KnightCheck" (next) (mover) E S)
                ("KnightCheck" (next) (mover) E N)
            })
            (and {
                (forEach Site (difference
                    (union {
                        (sites {(ahead (ahead (where "King" (next)) steps:2 N) steps:1 E)})
                        (sites {(ahead (ahead (where "King" (next)) steps:2 N) steps:1 W)})
                        (sites {(ahead (ahead (where "King" (next)) steps:2 S) steps:1 E)})
                        (sites {(ahead (ahead (where "King" (next)) steps:2 S) steps:1 W)})
                        (sites {(ahead (ahead (where "King" (next)) steps:1 N) steps:2 E)})
                        (sites {(ahead (ahead (where "King" (next)) steps:1 N) steps:2 W)})
                        (sites {(ahead (ahead (where "King" (next)) steps:1 S) steps:2 E)})
                        (sites {(ahead (ahead (where "King" (next)) steps:1 S) steps:2 W)})
                    })
                    (sites Occupied by:Next))
                    
                    (remember Value "CheckingPieceSite" (site) unique:True)
                )
                (note "Knight Check")
            })
        )
    })
)

(define "KingNextToOpposingKing"
    (or {
        (= (ahead (where "King" #1) steps:1 N) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 S) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 E) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 W) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 NE) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 NW) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 SE) (where "King" #2))
        (= (ahead (where "King" #1) steps:1 SW) (where "King" #2))
    })
)

(define "KingInCheck"
    (or {
        (or
            ("DiagonalCheck" #1 #2 SE)
            (and
                (= #1 2)
                ("PawnDiagonalCheck" #1 #2 SE)
            )
        )
        (or
            ("DiagonalCheck" #1 #2 SW)
            (and
                (= #1 2)
                ("PawnDiagonalCheck" #1 #2 SW)
            )
        )
        (or
            ("DiagonalCheck" #1 #2 NE)
            (and
                (= #1 1)
                ("PawnDiagonalCheck" #1 #2 NE)
            )
        )
        (or
            ("DiagonalCheck" #1 #2 NW)
            (and
                (= #1 1)
                ("PawnDiagonalCheck" #1 #2 NW)
            )
        )
        ("FileCheck" #1 #2)
        ("RankCheck" #1 #2)
        ("KnightCheck" #1 #2 N E)
        ("KnightCheck" #1 #2 N W)
        ("KnightCheck" #1 #2 W N)
        ("KnightCheck" #1 #2 W S)
        ("KnightCheck" #1 #2 S W)
        ("KnightCheck" #1 #2 S E)
        ("KnightCheck" #1 #2 E S)
        ("KnightCheck" #1 #2 E N)
        ("KingNextToOpposingKing" #1 #2)
    })
)

(define "CanPawnCapture"
    (and {
        (is Friend (who at:(ahead #1 #2)))
        (can Move
            (do
                (move (from: #1) (to: (ahead #1 #2)))
                ifAfterwards:(not ("KingInCheck" (next) (mover)))
            )
        )
    })
)

(define "CountTries"
    (and {
        (set Score Next 0)
        (forEach Site (sites Occupied by:Next "Pawn")
            (and {
                (if ("CanPawnCapture" (site) FR)
                    (addScore Next 1)
                )
                (if ("CanPawnCapture" (site) FL)
                    (addScore Next 1)
                )
            })
        )
    })
)

(define "SafePassingLocation"
    (and
        (is Empty (#1))
        (can Move
            (do
                (move
                    (from (where "King" Mover))
                    (to (#1))
                )
                ifAfterwards:(not ("KingInCheck" (mover) (next)))
            )
        )
    )
)

(define "KnightMovement"
    (and {
        (= (last To) (ahead (ahead (last From) steps:2 #1) steps:1 #2))
        (!= (ahead (last From) steps:2 #1) (ahead (ahead (last From) steps:2 #1) steps:1 #2))
        (!= (last From) (ahead (last From) steps:2 #1))
    })
)

(define "OrthogonalMovement"
    (or {
        (= (regionSite (sites LineOfSight Piece at:(last To) N) index: 0) (last From))
        (= (regionSite (sites LineOfSight Piece at:(last To) S) index: 0) (last From))
        (= (regionSite (sites LineOfSight Piece at:(last To) W) index: 0) (last From))
        (= (regionSite (sites LineOfSight Piece at:(last To) E) index: 0) (last From))
    })
)

(define "DiagonalMovement"
    (or {
        (= (regionSite (sites LineOfSight Piece at:(last To) NE) index: 0) (last From))
        (= (regionSite (sites LineOfSight Piece at:(last To) NW) index: 0) (last From))
        (= (regionSite (sites LineOfSight Piece at:(last To) SE) index: 0) (last From))
        (= (regionSite (sites LineOfSight Piece at:(last To) SW) index: 0) (last From))
    })
)

(define "KingMovement"
    (or {
        (= (ahead (last From) steps:1 N) (last To))
        (= (ahead (last From) steps:1 S) (last To))
        (= (ahead (last From) steps:1 E) (last To))
        (= (ahead (last From) steps:1 W) (last To))
        (= (ahead (last From) steps:1 NE) (last To))
        (= (ahead (last From) steps:1 NW) (last To))
        (= (ahead (last From) steps:1 SE) (last To))
        (= (ahead (last From) steps:1 SW) (last To))
    })
)

(define "DecideToCastle"
    (and {
        (= (ahead (last From) steps:#1 #2) (last To))
        (not ("KingInCheck" (mover) (next)))
        ("HasNeverMoved" "King")
        ("HasNeverMoved" #3)
        (is Empty (last To))
    })
)

(define "PawnStep"
    (and
        (= (last To) (ahead (last From) steps: 1 Forward))
        (is Empty (last To))
    )
)

(define "InitialPawnMove"
    (and {
        (= (last To) (ahead (last From) steps: 2 Forward))
        (is Empty (last To))
        (is In (last From) (sites Start (piece (what at:(last From)))))
        (or
            (= (regionSite (sites LineOfSight Piece at:(last To) N) index: 0) (last From))
            (= (regionSite (sites LineOfSight Piece at:(last To) S) index: 0) (last From))
        )
    })
)

(define "MoveForwardDiagonal"
    (and {
        (or
            (= (last To) (ahead (last From) steps: 1 FR))
            (= (last To) (ahead (last From) steps: 1 FL))
        )
        (or
            (is Enemy (who at:(last To)))
            (and
                (is Pending)
                (= (last To) (value Pending))
            )
        )
    })
)

(define "IsMovingPiece"
    (= (what at: (last From)) (mapEntry #1 Mover))
)

(define "MovingTheKing"
    (= (last From) (where "King" Mover))
)

(define "LegalMove"
    (and
        (can Move
            (do
                (move (from (last From)) (to (last To)))
                ifAfterwards:(not ("KingInCheck" (mover) (next)))
            )
        )
        (or {
            (and
                ("IsMovingPiece" "Pawn")
                (or {
                    "PawnStep"
                    "InitialPawnMove"
                    "MoveForwardDiagonal"
                })
            )
            (and
                ("IsMovingPiece" "Knight")
                (or {
                    ("KnightMovement" N E)
                    ("KnightMovement" N W)
                    ("KnightMovement" W N)
                    ("KnightMovement" W S)
                    ("KnightMovement" S E)
                    ("KnightMovement" S W)
                    ("KnightMovement" E N)
                    ("KnightMovement" E S)
                })
            )
            (and
                ("IsMovingPiece" "Queen")
                (or {
                    ("DiagonalMovement")
                    ("OrthogonalMovement")
                })
            )
            (and
                ("IsMovingPiece" "Rook")
                (or {
                    ("OrthogonalMovement")
                })
            )
            (and
                ("IsMovingPiece" "Bishop")
                (or {
                    ("DiagonalMovement")
                })
            )
            (and
                ("MovingTheKing")
                (or {
                    ("KingMovement")
                    (and {
                        ("DecideToCastle" 2 E "RookRight")
                        ("SafePassingLocation" (ahead (last From) steps: 1 E))
                        ("SafePassingLocation" (ahead (last From) steps: 2 E))
                    })
                    (and {
                        ("DecideToCastle" 2 W "RookLeft")
                        ("SafePassingLocation" (ahead (last From) steps: 1 W))
                        ("SafePassingLocation" (ahead (last From) steps: 2 W))
                        (is Empty (ahead (last From) steps: 3 W))
                    })
                })
            )
        })
    )
)

(define "CommonPostMoveActions"
    (and {
        (if ("KingInCheck" (next) (mover))
            (and {
                "CheckType"
                (if ("NextCanNotMove")
                    (set Var "NextLost" 1)
                )
            })
        )
        (if (= (var "DrawCondition") 0)
            (if (not ("KingInCheck" (next) (mover)))
                (if ("NextCanNotMove")
                    (and {
                        (set Var "DrawCondition" 1)
                        (note "Draw by stalemate")
                    })
                )
            )
        )
        (if (= (var "DrawCondition") 0)
            (if (= (counter) 99)
                (and {
                    (set Var "DrawCondition" 1)
                    (note "Draw by fifty-move rule")
                })
            )
        )
        (if (= (var "DrawCondition") 0)
            (if "OnlyKingsRemaining"
                (and {
                    (set Var "DrawCondition" 1)
                    (note "Draw by insufficient material: kings only")
                })
            )
        )
        (if (= (var "DrawCondition") 0)
            (if "KingVsKingAndSingleBishop"
                (and {
                    (set Var "DrawCondition" 1)
                    (note "Draw by insufficient material: king and bishop vs king")
                })
            )
        )
        (if (= (var "DrawCondition") 0)
            (if "KingVsKingAndSingleKnight"
                (and {
                    (set Var "DrawCondition" 1)
                    (note "Draw by insufficient material: king and knight vs king")
                })
            )
        )
        (if (= (var "DrawCondition") 0)
            (if "KingsAndBishopsPerSide"
                (and {
                    (set Var "DrawCondition" 1)
                    (note "Draw by insufficient material: kings and bishops")
                })
            )
        )
        "CountTries"
        ("ResetIllegalMovesList")
        (set Var "EquipmentMode" 0)
    })
)

(define "SetEnPassantLocation"
    (set Pending (ahead (last To) Backward))
)

(define "CountEnPassantTries"
    (if (= (what at: (ahead (last To) Rightward)) (next))
        (addScore Next 1)
    )
    (if (= (what at: (ahead (last To) Leftward)) (next))
        (addScore Next 1)
    )
)

(define "PerformLegalMove"
    (or {
        (if (and ("IsMovingPiece" "Pawn") ("PawnStep"))
            (move
                (from (last From))
                (to (last To))
                (then
                    (and {
                        (if (is In (last To) (sites Mover "Promotion"))
                            (moveAgain)
                        )
                        "CommonPostMoveActions"
                        (set Counter)
                    })
                )
            )
        )
        (if (and ("IsMovingPiece" "Pawn") (is Enemy (who at:(last To))))
            (and {
                (set Var "CapturedPiece" (what at:(last To)))
                (move
                    (from (last From))
                    (to (last To))
                    (then
                        (and {
                            ("NoteCapture" (last To) (var "CapturedPiece"))
                            (if (is In (last To) (sites Mover "Promotion"))
                                (moveAgain)
                            )
                            "CommonPostMoveActions"
                            (set Counter)
                        })
                    )
                )
            })
        )
        (if (and {
                ("IsMovingPiece" "Pawn")
                (is Pending)
                (= (last To) (value Pending))
            })
            (move
                (from (last From))
                (to (last To))
                (then
                    (and {
                        (set Var "CapturedPiece"(what at:(ahead (last To) Backward)))
                        (set Var "CaptureLocation" (ahead (last To) Backward))
                        (remove
                            (ahead (last To) Backward)
                            (then
                                (and {
                                    ("NoteCapture" (var "CaptureLocation") (var "CapturedPiece"))
                                    "CommonPostMoveActions"
                                    (set Counter)
                                })
                            )
                        )
                    })
                )
            )
        )
        (if (and ("IsMovingPiece" "Pawn") ("InitialPawnMove"))
            (move
                (from (last From))
                (to (last To))
                (then
                    (and {
                        "SetEnPassantLocation"
                        "CommonPostMoveActions"
                        "CountEnPassantTries"
                        (set Counter)
                    })
                )
            )
        )
        (if (and
                (or {
                    ("IsMovingPiece" "Knight") ("IsMovingPiece" "Queen")
                    ("IsMovingPiece" "Rook") ("IsMovingPiece" "Bishop")
                })
                (is Enemy (who at:(last To)))
            )
            (and
                (set Var "CapturedPiece" (what at:(last To)))
                (move
                    (from (last From))
                    (to (last To))
                    (then
                        (and {
                            ("NoteCapture" (last To) (var "CapturedPiece"))
                            "CommonPostMoveActions"
                            ("PieceHasMoved" (last To))
                            (set Counter)
                        })
                    )
                )
            )
        )
        (if (and
                (or {
                    ("IsMovingPiece" "Knight") ("IsMovingPiece" "Queen")
                    ("IsMovingPiece" "Rook") ("IsMovingPiece" "Bishop")
                })
                (is Empty (last To))
            )
            (move
                (from (last From))
                (to (last To))
                (then
                    (and {
                        "CommonPostMoveActions"
                        ("PieceHasMoved" (last To))
                    })
                )
            )
        )
        (if (and ("MovingTheKing") (is Enemy (who at:(last To))))
            (and
                (set Var "CapturedPiece" (what at:(last To)))
                (move
                    (from (last From))
                    (to (last To))
                    (then
                        (and {
                            ("NoteCapture" (last To) (var "CapturedPiece"))
                            "CommonPostMoveActions"
                            ("PieceHasMoved" (last To))
                            (set Counter)
                        })
                    )
                )
            )
        )
        (if (and ("MovingTheKing") (is Empty (last To)))
            (move
                (from (last From))
                (to (last To))
                (then
                    (and {
                        "CommonPostMoveActions"
                        ("PieceHasMoved" (last To))
                    })
                )
            )
        )
        (if (and ("MovingTheKing") ("DecideToCastle" 2 E "RookRight"))
            (and
                (move (from (last From)) (to (last To)) (then ("PieceHasMoved" (last To))))
                (move
                    (from (mapEntry "RookRight" (mover)))
                    (to (ahead (mapEntry "RookRight" (mover)) steps: 2 W))
                    (then
                        (and {
                            "CommonPostMoveActions"
                            ("PieceHasMoved" (ahead (mapEntry "RookRight" (mover)) steps: 2 W))
                        })
                    )
                )
            )
        )
        (if (and ("MovingTheKing") ("DecideToCastle" 2 W "RookLeft"))
            (and
                (move (from (last From)) (to (last To)) (then ("PieceHasMoved" (last To))))
                (move
                    (from (mapEntry "RookLeft" (mover)))
                    (to (ahead (mapEntry "RookLeft" (mover)) steps: 3 E))
                    (then
                        (and {
                            "CommonPostMoveActions"
                            ("PieceHasMoved" (ahead (mapEntry "RookLeft" (mover)) steps: 3 E))
                        })
                    )
                )
            )
        )
    })
)

(define "InLocationEnPassant"
    (and
        (is Pending)
        (= (to) (value Pending))
    )
)

(define "EnPassant"
    (move
        Step
        (directions {FR FL})
        (to if:"InLocationEnPassant")
        (then
            (remove
                (ahead (last To) Backward)
            )
        )
    )
)

(define "PromotionCondition"
    (and
        (is In (last To) (sites Mover "Promotion"))
        (= (what at: (last To)) (mapEntry "Pawn" Mover))
    )
)

(define "AttemptDoubleStepForward"
    (move Slide (directions Forward) (between (exact 2)) (to if:(not (is Friend (who at:(to))))))
)

(define "AttemptStepForward"
    (move Step (directions Forward) (to if:(not (is Friend (who at:(to))))))
)

(define "AttemptCaptureForwardDiagonal"
    (move Step (directions {FR FL}) (to if:(not (is Friend (who at:(to))))))
)

(define "AttemptSlide"
    (move Select
        (from (from))
        (to
            (sites Direction
                from:(from)
                #1
                stop:(is Friend (who at:(to)))
                stopIncluded:False
            )
            if:(not (is Friend (who at:(to))))
        )
    )
)

(define "KriegspielRook"
    (piece "Rook" Each
        (if (not (= (var "EquipmentMode") 1))
            ("AttemptSlide" Orthogonal)

            (move Slide Orthogonal (to if:(is Enemy (who at:(to))) (apply (remove (to)))))
        )
    )
)

(define "KriegspielBishop"
    (piece "Bishop" Each
        (if (not (= (var "EquipmentMode") 1))
            ("AttemptSlide" Diagonal)

            (move Slide Diagonal (to if:(is Enemy (who at:(to))) (apply (remove (to)))))
        )
    )
)

(define "KriegspielQueen"
    (piece "Queen" Each
        (if (not (= (var "EquipmentMode") 1))
            ("AttemptSlide" (union Orthogonal Diagonal))

            (move Slide (to if:(is Enemy (who at:(to))) (apply (remove (to)))))
        )
    )
)

(define "KriegspielPawn"
    (piece "Pawn" Each
        (if (not (= (var "EquipmentMode") 1))
            (or {
                "AttemptStepForward"
                (if (is In (from) (sites Start (piece (what at:(from))))) "AttemptDoubleStepForward")
                (if (> (score Mover) 0) "AttemptCaptureForwardDiagonal")
            })

            (or {
                "StepForwardToEmpty"
                (if (is In (from) (sites Start (piece (what at:(from))))) "DoubleStepForwardToEmpty")
                ("StepToEnemy" (directions {FR FL}))
                "EnPassant"
            })
        )
    )
)

(define "KriegspielKing"
    (piece "King" Each
        (or {
            (move Step All (to if:(not (is Friend (who at:(to)))) (apply (if (is Enemy (who at:(to))) (remove (to))))))
            (if (= (from) (where "King" Mover))
                (if (not (= (var "EquipmentMode") 1))
                    (or {
                        (move (from) (to (ahead (from) steps:2 W) if:(and {
                            ("HasNeverMoved" "King")
                            ("HasNeverMoved" "RookLeft")
                            (not (is Friend (who at:(ahead (from) steps:1 W))))
                            (not (is Friend (who at:(ahead (from) steps:2 W))))
                            (not (is Friend (who at:(ahead (from) steps:3 W))))
                        })))
                        (move (from) (to (ahead (from) steps:2 E) if:(and {
                            ("HasNeverMoved" "King")
                            ("HasNeverMoved" "RookRight")
                            (not (is Friend (who at:(ahead (from) steps:1 E))))
                            (not (is Friend (who at:(ahead (from) steps:2 E))))
                        })))
                    })

                    (or {
                        (move (from) (to (ahead (from) steps:2 W) if:(and {
                            ("HasNeverMoved" "King")
                            ("HasNeverMoved" "RookLeft")
                            (is Empty (ahead (from) steps:1 W))
                            (is Empty (ahead (from) steps:2 W))
                            (is Empty (ahead (from) steps:3 W))
                        })))
                        (move (from) (to (ahead (from) steps:2 E) if:(and {
                            ("HasNeverMoved" "King")
                            ("HasNeverMoved" "RookRight")
                            (is Empty (ahead (from) steps:1 E))
                            (is Empty (ahead (from) steps:2 E))
                        })))
                    })
                )
            )
        })
    )
)

(define "OnlyKingsRemaining"
    (and {
        (= (count Pieces) 2)
        (= (count Pieces "King") 2)
    })
)

(define "KingVsKingAndSingleBishop"
    (and {
        (= (count Pieces) 3)
        (= (count Pieces "King") 2)
        (= (count Pieces "Bishop") 1)
    })
)

(define "KingVsKingAndSingleKnight"
    (and {
        (= (count Pieces) 3)
        (= (count Pieces "King") 2)
        (= (count Pieces "Knight") 1)
    })
)

(define "KingsAndBishopsPerSide"
    (and {
        (= (count Pieces) 4)
        (= (count Pieces "King") 2)
        (= (count Pieces "Bishop") 2)
        (= (count Pieces P1 "Bishop") 1)
        (= (count Pieces P2 "Bishop") 1)
    })
)

(define "ResetIllegalMovesList"
    (and {
        (forget Value "IllegalMovesP1" All)
        (forget Value "IllegalMovesP2" All)
    })
)

(define "RememberIllegalMove"
    (and {
        (if (= (mover) 1)
            (remember Value "IllegalMovesP1" (+ (* (last From) 100) (last To)) unique:True)
        )
        (if (= (mover) 2)
            (remember Value "IllegalMovesP2" (+ (* (last From) 100) (last To)) unique:True)
        )
    })
)

(define "CanMove"
    (or {
        (is In (last From) (values Remembered "CheckedKingSite"))
        (= 0 (size Array (values Remembered "CheckingPieceSite")))
        (is In (last To) (values Remembered "CheckingPieceSite"))
    })
)

(define "GenerateValidMoves"
    (move Select
        (from (sites Occupied by:Mover))
        (to
            (sites To
                (do
                    (forEach Piece)
                    ifAfterwards:(and {
                        ("CanMove")
                        (= (from) (last From))
                        (if (= (mover) 1)
                            (not
                                (is In
                                    (+ (* (from) 100) (last To))
                                    (values Remembered "IllegalMovesP1")
                                )
                            )

                            (not
                                (is In
                                    (+ (* (from) 100) (last To))
                                    (values Remembered "IllegalMovesP2")
                                )
                            )
                        )
                    })
                )
            )
        )
    )
)

(game "Kriegspiel (Chess)"
    (players {(player N) (player S)})
    (equipment {
        (board (square 8))
        ("KriegspielPawn")
        ("KriegspielRook")
        ("KriegspielKing")
        ("KriegspielBishop")
        ("ChessKnight" "Knight")
        ("KriegspielQueen")
        (map "King" {(pair 1 "E1") (pair 2 "E8")})
        (map "Queen" {(pair 1 11) (pair 2 12)})
        (map "Rook" {(pair 1 3) (pair 2 4)})
        (map "Bishop" {(pair 1 7) (pair 2 8)})
        (map "Knight" {(pair 1 9) (pair 2 10)})
        (map "Pawn" {(pair 1 1) (pair 2 2)})
        (map "RookLeft" {(pair 1 "A1") (pair 2 "A8")})
        (map "RookRight" {(pair 1 "H1") (pair 2 "H8")})
        (map "CaptureNotePawn"
            {
                (pair (coord:"A1") "Pawn at A1 captured")
                (pair (coord:"A2") "Pawn at A2 captured")
                (pair (coord:"A3") "Pawn at A3 captured")
                (pair (coord:"A4") "Pawn at A4 captured")
                (pair (coord:"A5") "Pawn at A5 captured")
                (pair (coord:"A6") "Pawn at A6 captured")
                (pair (coord:"A7") "Pawn at A7 captured")
                (pair (coord:"A8") "Pawn at A8 captured")
                (pair (coord:"B1") "Pawn at B1 captured")
                (pair (coord:"B2") "Pawn at B2 captured")
                (pair (coord:"B3") "Pawn at B3 captured")
                (pair (coord:"B4") "Pawn at B4 captured")
                (pair (coord:"B5") "Pawn at B5 captured")
                (pair (coord:"B6") "Pawn at B6 captured")
                (pair (coord:"B7") "Pawn at B7 captured")
                (pair (coord:"B8") "Pawn at B8 captured")
                (pair (coord:"C1") "Pawn at C1 captured")
                (pair (coord:"C2") "Pawn at C2 captured")
                (pair (coord:"C3") "Pawn at C3 captured")
                (pair (coord:"C4") "Pawn at C4 captured")
                (pair (coord:"C5") "Pawn at C5 captured")
                (pair (coord:"C6") "Pawn at C6 captured")
                (pair (coord:"C7") "Pawn at C7 captured")
                (pair (coord:"C8") "Pawn at C8 captured")
                (pair (coord:"D1") "Pawn at D1 captured")
                (pair (coord:"D2") "Pawn at D2 captured")
                (pair (coord:"D3") "Pawn at D3 captured")
                (pair (coord:"D4") "Pawn at D4 captured")
                (pair (coord:"D5") "Pawn at D5 captured")
                (pair (coord:"D6") "Pawn at D6 captured")
                (pair (coord:"D7") "Pawn at D7 captured")
                (pair (coord:"D8") "Pawn at D8 captured")
                (pair (coord:"E1") "Pawn at E1 captured")
                (pair (coord:"E2") "Pawn at E2 captured")
                (pair (coord:"E3") "Pawn at E3 captured")
                (pair (coord:"E4") "Pawn at E4 captured")
                (pair (coord:"E5") "Pawn at E5 captured")
                (pair (coord:"E6") "Pawn at E6 captured")
                (pair (coord:"E7") "Pawn at E7 captured")
                (pair (coord:"E8") "Pawn at E8 captured")
                (pair (coord:"F1") "Pawn at F1 captured")
                (pair (coord:"F2") "Pawn at F2 captured")
                (pair (coord:"F3") "Pawn at F3 captured")
                (pair (coord:"F4") "Pawn at F4 captured")
                (pair (coord:"F5") "Pawn at F5 captured")
                (pair (coord:"F6") "Pawn at F6 captured")
                (pair (coord:"F7") "Pawn at F7 captured")
                (pair (coord:"F8") "Pawn at F8 captured")
                (pair (coord:"G1") "Pawn at G1 captured")
                (pair (coord:"G2") "Pawn at G2 captured")
                (pair (coord:"G3") "Pawn at G3 captured")
                (pair (coord:"G4") "Pawn at G4 captured")
                (pair (coord:"G5") "Pawn at G5 captured")
                (pair (coord:"G6") "Pawn at G6 captured")
                (pair (coord:"G7") "Pawn at G7 captured")
                (pair (coord:"G8") "Pawn at G8 captured")
                (pair (coord:"H1") "Pawn at H1 captured")
                (pair (coord:"H2") "Pawn at H2 captured")
                (pair (coord:"H3") "Pawn at H3 captured")
                (pair (coord:"H4") "Pawn at H4 captured")
                (pair (coord:"H5") "Pawn at H5 captured")
                (pair (coord:"H6") "Pawn at H6 captured")
                (pair (coord:"H7") "Pawn at H7 captured")
                (pair (coord:"H8") "Pawn at H8 captured")
            }
        )
        (map "CaptureNotePiece"
            {
                (pair (coord:"A1") "Piece at A1 captured")
                (pair (coord:"A2") "Piece at A2 captured")
                (pair (coord:"A3") "Piece at A3 captured")
                (pair (coord:"A4") "Piece at A4 captured")
                (pair (coord:"A5") "Piece at A5 captured")
                (pair (coord:"A6") "Piece at A6 captured")
                (pair (coord:"A7") "Piece at A7 captured")
                (pair (coord:"A8") "Piece at A8 captured")
                (pair (coord:"B1") "Piece at B1 captured")
                (pair (coord:"B2") "Piece at B2 captured")
                (pair (coord:"B3") "Piece at B3 captured")
                (pair (coord:"B4") "Piece at B4 captured")
                (pair (coord:"B5") "Piece at B5 captured")
                (pair (coord:"B6") "Piece at B6 captured")
                (pair (coord:"B7") "Piece at B7 captured")
                (pair (coord:"B8") "Piece at B8 captured")
                (pair (coord:"C1") "Piece at C1 captured")
                (pair (coord:"C2") "Piece at C2 captured")
                (pair (coord:"C3") "Piece at C3 captured")
                (pair (coord:"C4") "Piece at C4 captured")
                (pair (coord:"C5") "Piece at C5 captured")
                (pair (coord:"C6") "Piece at C6 captured")
                (pair (coord:"C7") "Piece at C7 captured")
                (pair (coord:"C8") "Piece at C8 captured")
                (pair (coord:"D1") "Piece at D1 captured")
                (pair (coord:"D2") "Piece at D2 captured")
                (pair (coord:"D3") "Piece at D3 captured")
                (pair (coord:"D4") "Piece at D4 captured")
                (pair (coord:"D5") "Piece at D5 captured")
                (pair (coord:"D6") "Piece at D6 captured")
                (pair (coord:"D7") "Piece at D7 captured")
                (pair (coord:"D8") "Piece at D8 captured")
                (pair (coord:"E1") "Piece at E1 captured")
                (pair (coord:"E2") "Piece at E2 captured")
                (pair (coord:"E3") "Piece at E3 captured")
                (pair (coord:"E4") "Piece at E4 captured")
                (pair (coord:"E5") "Piece at E5 captured")
                (pair (coord:"E6") "Piece at E6 captured")
                (pair (coord:"E7") "Piece at E7 captured")
                (pair (coord:"E8") "Piece at E8 captured")
                (pair (coord:"F1") "Piece at F1 captured")
                (pair (coord:"F2") "Piece at F2 captured")
                (pair (coord:"F3") "Piece at F3 captured")
                (pair (coord:"F4") "Piece at F4 captured")
                (pair (coord:"F5") "Piece at F5 captured")
                (pair (coord:"F6") "Piece at F6 captured")
                (pair (coord:"F7") "Piece at F7 captured")
                (pair (coord:"F8") "Piece at F8 captured")
                (pair (coord:"G1") "Piece at G1 captured")
                (pair (coord:"G2") "Piece at G2 captured")
                (pair (coord:"G3") "Piece at G3 captured")
                (pair (coord:"G4") "Piece at G4 captured")
                (pair (coord:"G5") "Piece at G5 captured")
                (pair (coord:"G6") "Piece at G6 captured")
                (pair (coord:"G7") "Piece at G7 captured")
                (pair (coord:"G8") "Piece at G8 captured")
                (pair (coord:"H1") "Piece at H1 captured")
                (pair (coord:"H2") "Piece at H2 captured")
                (pair (coord:"H3") "Piece at H3 captured")
                (pair (coord:"H4") "Piece at H4 captured")
                (pair (coord:"H5") "Piece at H5 captured")
                (pair (coord:"H6") "Piece at H6 captured")
                (pair (coord:"H7") "Piece at H7 captured")
                (pair (coord:"H8") "Piece at H8 captured")
            }
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules
        (start {
            (place "Pawn1" (sites Row 1))
            (place "Rook1" {"A1" "H1"} state:1)
            (place "Knight1" {"B1" "G1"})
            (place "Bishop1" {"C1" "F1"})
            (place "Queen1" coord:"D1")
            (place "King1" coord:"E1" state:1)
            (place "Pawn2" (sites Row 6))
            (place "Rook2" {"A8" "H8"} state:1)
            (place "Knight2" {"B8" "G8"})
            (place "Bishop2" {"C8" "F8"})
            (place "Queen2" coord:"D8")
            (place "King2" coord:"E8" state:1)
            (set Hidden (sites Occupied by: Mover) to:Next)
            (set Hidden (sites Occupied by: Next) to:Mover)
        })
        (play
            (and
                (if "PromotionCondition"
                    (move Promote (last To) (piece {"Queen" "Knight" "Bishop" "Rook"}) Mover
                        (then
                            ("CommonPostMoveActions")
                        )
                    )
                )
                (if (or
                        ("NewTurn")
                        (not "PromotionCondition")
                    )
                    ("GenerateValidMoves")
                    (then
                        (and {
                            (priority
                                (if "LegalMove"
                                    (and {
                                        (set Var "EquipmentMode" 1)
                                        "PerformLegalMove"
                                        (forget Value "CheckedKingSite" All)
                                        (forget Value "CheckingPieceSite" All)
                                    })
                                )
                                (and {
                                    ("RememberIllegalMove")
                                    (note "Illegal move")
                                    (moveAgain)
                                })
                            )
                        })
                    )
                )
            )
        )
        (end {
            (if (= (var "NextLost") 1) (result Mover Win))
            (if (= (var "DrawCondition") 1) (result Mover Draw))
        })
    )
)

(metadata
    (info
        {
        (description "In this game each player can see their own pieces, but not those of their opponent. For this reason, it is necessary to have a third person (or computer) act as an umpire, with full information about the progress of the game. Players attempt to move on their turns, and the umpire declares their attempts 'legal' or 'illegal'. If the move is illegal, the player tries again; if it is legal, that move stands. Each player is given information about checks and captures. Since the position of the opponent's pieces is unknown, Kriegspiel is a game of imperfect information. This implementation suppresses any move that fails feasibility checks, so 'Hell no' announcements for impossible attempts never occur and the agents explore a tighter decision space.")
        (aliases {"Blind Chess" "Wild 16"})
        (rules "The game is played with three boards, one for each player; the third is for the umpire (and spectators). Each opponent knows the exact position of just their own pieces, and does not know where the opponent's pieces are (but can keep track of how many there are). Only the umpire knows the position of the game. The game proceeds in the following way:
            The umpire announces:
            -'White's [or Black's] turn'.
            -'Pawn at (square) captured', when a pawn is captured. The square of the capture is announced, e.g. Pawn gone on d4, or Pawn captured on d4. (En passant captures are specifically announced as such.)
            -'Piece at (square) captured', when a piece is captured. The square of the capture is announced.
            -'Illegal move' when the attempted move is illegal, given the opponent's position. For example: moving the king into check; moving a queen, rook, bishop, or pawn through squares occupied by the opponent's pieces; advancing a pawn into a square occupied by the opponent's pieces; moving a piece under an absolute pin.
            -'Rank check'.
            -'File check'.
            -'Long-diagonal check' (the longer of the two diagonals, from the king's point of view).
            -'Short-diagonal check'.
            -'Knight check'.
            -'(number)  tries' (number of legal capturing moves using pawns).
            -'Checkmate', 'stalemate', 'draw by insufficient force', '50-move draw'. (Draw by repetition is intentionally excluded in this version.)
            Pawn promotions are not announced. The precise location of the checking piece is not announced (although it may be deduced).
            Illegal move attempts are not announced to the opponent. As soon as the umpire rejects one it disappears from the moverâ€™s options for the rest of that turn, eliminating wasted retries."
        )
        (id "4185")
        (source "<a href=\"https://en.wikipedia.org/wiki/Kriegspiel_(chess)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.14")
        (classification "board/war/replacement/checkmate/chess")
        (author "Henry Michael Temple")
        (credit "Nikola Novarlic & Fabio Chiarini")
        (date "1899")
        }
    )
    (graphics {
        (show Score Always " tries")
        (piece Scale "Pawn" 0.825)
        (piece Families {"Defined" "Microsoft" "Pragmata" "Symbola"})
        (board Style Chess)
    })
)

